import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'multiloader-loader'
    id 'net.neoforged.gradle.userdev' version '7.0.142'
    id 'net.darkhax.curseforgegradle' version '1.+'
    id 'com.modrinth.minotaur' version '2.+'
}

base {
    archivesName = "${mod_id}-${project.name}-${neoforge_supported_versions}"
}

runs {
    configureEach {
        modSource project.sourceSets.main
    }
    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }
    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }
    data {
        programArguments.addAll'--mod', project.mod_id, '--all',
                '--output', file('src/generated/resources/').getAbsolutePath(),
                '--existing', file('src/main/resources/').getAbsolutePath(),
                '--existing', file("${project(':common').projectDir}/src/main/resources/").getAbsolutePath()
    }
}

sourceSets.main.resources { srcDirs = ['src/main/resources', 'src/generated/resources'] }

subsystems {
    parchment {
        minecraftVersion = "${parchment_mc}"
        mappingsVersion = "${parchment_version}"
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"

    // CATALOGUE
    if (project.hasProperty('catalogue_version')) {
        runtimeOnly "curse.maven:catalogue-459701:${catalogue_version}"
    }

    // JADE
    if (project.hasProperty('jade_version')) {
        runtimeOnly "curse.maven:jade-324717:${jade_version}"
    }

    // JEI
    if (project.hasProperty('jei_version')) {
        if (project.hasProperty('enable_jei') && project.enable_jei.toBoolean()) {
            runtimeOnly("mezz.jei:jei-${neoforge_minecraft_version}-neoforge:${jei_version}")
        }
    }

    implementation "maven.modrinth:industrial-foregoing:1.21-3.6.15"
    implementation "maven.modrinth:titanium:1.21-4.0.21"

}

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = file('../../API Tokens/mifa.md')
    def projectId = "${cf_id}".toString()
    def mainFile = upload(projectId, file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")) as Object
    mainFile.changelogType = 'markdown'
    mainFile.changelog = file('changelog.md')
    mainFile.addGameVersion('Client', 'Server')
    mainFile.releaseType = "${release_type}"
    "${neoforge_release_versions}".split(',').each {
        mainFile.addGameVersion("${it}")
    }
    mainFile.addModLoader('NeoForge')
    mainFile.addRequirement("industrial-foregoing")
}

modrinth {
    def tokenText = file('../../API Tokens/modrinth.md')
    if (tokenText.exists()) {
        token = tokenText.text
    } else {
        token = ""
    }
    projectId = "${modrinth_id}"
    versionName = getArchivesBaseName() + "-" + getVersion()
    versionType = "${release_type}"
    changelog = file('changelog.md').text
    uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
    gameVersions = "${neoforge_release_versions}".split(",") as List<String>
    dependencies {
        required.project "industrial-foregoing"
    }
}